plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def localProperties = new Properties()

if (rootProject.file("local.properties").exists()) {
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))
} else {
    localProperties.setProperty('movieDbApiKey',"\"API_KEY_STUB\"")
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField "String", "MOVIE_DB_API_KEY", localProperties['movieDbApiKey']
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    // for robolectric testing
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

dependencies {
    def paging_version = "3.0.0-alpha12"
    def joda_version = "2.10.4"
    def room_version = "2.3.0-alpha04"
    def robolectric_version = "4.3.1"

    // Project modules
    implementation project (':features:reviews:appServices')
    implementation project (':features:reviews:domain')
    implementation project (':features:common:util')
    api project (':features:common:data')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation "androidx.paging:paging-rxjava2-ktx:$paging_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Javax annotation
    implementation "javax.inject:javax.inject:1"

    // Joda Time
    implementation "joda-time:joda-time:$joda_version"

    // Unit/Local testing

    // Common module
    testImplementation project(':features:common:localTesting')

    // Retrofit
    testImplementation "com.squareup.okhttp3:mockwebserver:3.9.1"
    testImplementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    testImplementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    // Paging
    testImplementation "androidx.paging:paging-common-ktx:$paging_version"

    // Android instrumentation(used by robolectric)
    testImplementation 'androidx.test.ext:junit:1.1.2'
    testImplementation 'androidx.test:rules:1.3.0'
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation "androidx.arch.core:core-testing:2.1.0"

    // Gson
    testImplementation 'com.google.code.gson:gson:2.8.6'

    // Robolectric
    testImplementation "org.robolectric:robolectric:$robolectric_version"

    // Room
    kaptTest "androidx.room:room-compiler:$room_version"
}